# Linux home-manager profile: base packages and the desktop applications.
# The desktop environments themselves live in modules/desktop/{cosmic,gnome,xfce}.nix
# (cross-class), selected by custom.features.graphical.desktopManager.
{
  flake.modules.homeManager.linux.imports = [
    (
      {
        pkgs,
        lib,
        ...
      }:
      {
        home.packages = with pkgs; [
          angrr
          cfspeedtest
          # https://unix.stackexchange.com/a/617686
          (writeShellApplication {
            name = "getusers";
            text = /* bash */ ''
              ${lib.getExe' procps "ps"} -eo user,uid | ${lib.getExe gawk} 'NR>1 && $2 >= 1000 && ++seen[$2]==1{print $1}'
            '';
          })
        ];
      }
    )

    (
      {
        pkgs,
        lib,
        config,
        ...
      }:
      lib.mkIf config.custom.features.graphical.enable {
        home.packages =
          with pkgs;
          [
            anydesk
            obsidian
            teams-for-linux
            zotero
          ]
          ++ lib.optionals (pkgs.stdenv.hostPlatform.isx86_64) [
            zoom-us
          ];
        home.file.".face".source = ./mlenz.jpg;
      }
    )
  ];
}
